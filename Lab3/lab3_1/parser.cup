import java_cup.runtime.*;

parser code {:

    public void report_error(String message, Object info) {
        System.err.print("ERROR: Syntax error");
        if (info instanceof Symbol)
            if (((Symbol)info).left != -1) {
                int line = (((Symbol)info).left) + 1;
                int column = (((Symbol)info).right) + 1;
                System.err.print(" (line " + line + ", column " + column + "): ");
            } else System.err.print(": ");
        else System.err.print(": ");
    }

:};

terminal INT_TYPE, DOUBLE_TYPE,
		 PRINT, IF, ELSE, WHILE,
		 RO, RC, SO, SC, BO, BC,
		 PLUS, MINUS, STAR, DIV, ASSIGN, S,
		 EQ, GREQ, GR, SMEQ, SM, AND, OR, NOT, CM,
		 INT, DOUBLE, ID,
		 UMINUS;

non terminal main,
			 decl_list, decl, type, var_list, var,
			 stmt_list, stmt, if, while, print, assignment,
			 id, index, expr, condition;

precedence left OR;
precedence left AND;
precedence left NOT;
precedence left EQ, GREQ, GR, SMEQ, SM;
precedence left PLUS, MINUS;
precedence left STAR, DIV;
precedence left UMINUS;

start with main;

main ::= decl_list stmt_list {: 
			System.out.println("Programm correctly recognised"); 
		 :};

decl_list ::= decl_list decl | ;
decl ::= type var_list S;
type ::= INT_TYPE | DOUBLE_TYPE;
var_list ::= var_list CM var | var;
var ::= ID SO INT SC | ID;

stmt_list ::= stmt_list stmt | ;
stmt ::= if | while | print | assignment | BO stmt_list BC;

assignment ::= id S | id ASSIGN expr S;
id ::= ID | ID SO index SC;
index ::= INT | ID;

if ::= IF condition stmt ELSE stmt | IF condition stmt;
while ::= WHILE condition stmt;
condition ::= RO expr RC;
print ::= PRINT RO id RC;

expr ::=  expr AND expr
		| expr OR expr
		| expr NOT expr
		| expr EQ expr
		| expr GREQ expr
		| expr GR expr
		| expr SMEQ expr
		| expr SM expr
		| expr PLUS expr
		| expr MINUS expr
		| expr STAR expr
		| expr DIV expr
		| RO expr RC
		| id
		| INT
		| DOUBLE
		| MINUS INT %prec UMINUS
		| MINUS DOUBLE %prec UMINUS; 
		 
